# -----------------------------------------------------------------
# Default Message Forwarder (anything except ACK/PRACK/CANCEL)
# -----------------------------------------------------------------

route[initial-forwarder] {
  xlog("L_DBG","DEBUG -- route(forwarder) Processing $rm $ru");

  if($(avp(dest_domain)))
  {
      xlog("L_DBG","DEBUG -- route(forwarder) Using destination $(avp(dest_domain))");
      $rd = $(avp(dest_domain));
  }

  serialize_branches(1);

  route(forwarder);
}

route[forwarder] {
    xlog("L_DBG","DEBUG -- route(forwarder) t_relay $rm $ru");

    if (!t_relay("0x01")) # Prevent dup 100
    {
        # t_relay failed, check if we need to stop mediaproxy
        if (is_method("INVITE")) {
            route(stop-media-proxy); # End media session
        };
        sl_send_reply("500","Transmission failure");
    };
    exit;
}

# -----------------------------------------------------------------
# Forward ACK, PRACK, and CANCEL
# -----------------------------------------------------------------

route[forwarder-progress]
{
    xlog("L_DBG","DEBUG -- route(forwarder-progress) ACK/PRACK/CANCEL Processing $rm $ru");

    if(!t_check_trans())
    {
        xlog("L_DBG", "DEBUG -- route(forwarder-progress) Dropping mis-routed $rm (481)");
        # Can't send a reply to an ACK
        if(!is_method("ACK"))
        {
            sl_send_reply("481", "Call/Transaction Does Not Exist");
        }
        exit;
    }

    xlog("L_DBG", "DEBUG -- route(forwarder-progress) t_relay $rm $ru");
    if(!t_relay())
    {
        # t_relay failed, check if we need to stop mediaproxy
        if (is_method("ACK")) {
            route(stop-media-proxy); # End media session
        };
        sl_send_reply("500","Transmission failure");
    };
    exit;
}

